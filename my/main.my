# --
# --  User-serviceable variables
# --

# TODO:  I have a major looming issue with regular expression search/replace
#        match for xx.*xx and replace with xxyyxx
#        I would expect xxzzxxzzxx => xxyyxxyyxx but instead I get xxyyxxzzxx (the second xxzzxx is not matched)


# TODO:  Configuration file.  See TODO.markdown
source_directory =  "/l/projects/compiled-website/git/asc/";
target_directory =  "/l/projects/compiled-website/git/html/";
# -rebuilt.html  will automatically be appended and used, after the search/replace.
header =  "/l/projects/compiled-website/git/my/header.html";
footer =  "/l/projects/compiled-website/git/my/footer.html";

site_name  = "<div class=\"top-t1\">S</div>&nbsp; <div class=\"top-t2\">piral of Hope</div>";
site_tagline =  "<div class=\"top-t3\">Better software is possible.</div>";


# TODO:  Library functionality.  It's been implemented in an easy way in Mythryl, but as of this writing that easy way is only available in the git version of Mythryl.


# --
# -- Library-candidate functions, testing-related
# --

# TODO:  should testing be able to throw errors?
# TODO:  Tallying:  Number of tests, passes and fails.  Time spent processing.
# TODO:  Numbers, stats, etc.
# TODO:  comparing Lists of Ints, etc?  I can't re-use test_assert_lists_of_strings_are_equal() if printf needs %s to display contents.  How do I deal with this?
# TODO:  Make an assert_not() which will flip TRUE=>FALSE and FALSE=>TRUE.  Then leverage that with a 'not' for each assert_foo test function.
#        For example, make assert_strings_are_not_equal() but leverage the existing assert_strings_are_equal() and pass it through assert_not() somehow or other.  You can do it.


# TODO:  Make a general "assert_equal() which can handle all types.
fun assert_strings_are_equal( expected: String,  result: String ):   Bool = {
  if ( expected == result )
    TRUE;
  else
    print "\n  ** FAIL **\n";
    print expected;
    print "\n  =>\n";
    print result;
    print "\n";
    FALSE;
  fi;
};
fun test_assert_strings_are_equal( _: Void ):  Bool = {
  string   =  "- bullet\nytesty\n";
  expected =  string;
  result   =  string;
  assert_strings_are_equal( expected, result );
};
# test_assert_strings_are_equal();


fun assert_lists_of_strings_are_equal( list_one: List( String ),  list_two: List( String ) ):  Bool = {
  result =  REF TRUE;
  i      =  REF -1;
  if ( list::length( list_one ) != list::length( list_two ) )
    result :=  FALSE;
    print "  # FAIL - Lists are different lengths.\n";


    print "    list_one (expected):\n";
    apply printf "'%s'\n" list_one;
    print "    list_two (result):\n";
    apply printf "'%s'\n" list_two;
  else
    # TRUE
    foreach list_one .{
      throwaway =  #e;
      # I could stop at the first failure with:
      # if ( *result == TRUE )
      i :=  *i + 1;
      if ( list::nth( list_one, *i ) != list::nth( list_two, *i ) )
        result :=  FALSE;
        # TODO:  A much more comprehensive display.
        print "  # FAIL - Mismatched element.\n";
        print ( "  expected:  '" + list::nth( list_one, *i ) + "'" );
        print "\n  =>\n";
        print ( "  result  :  '" + list::nth( list_two, *i ) + "'" );
        print "\n";
      fi;
    };
  fi;
  *result;
};
fun test_assert_lists_of_strings_are_equal( _: Void ):  Bool = {
  expected =  [ "one", "two" ];
  result   =  expected;
  assert_lists_of_strings_are_equal( expected, result );

  # Meant to fail, number of elements
  expected =  [ "- bullet", "ytesty" ];
  result   =  [ "- bullet", "ytesty", "number" ];
  assert_lists_of_strings_are_equal( expected, result );

  # Meant to fail, content
  expected =  [ "- bullet", "ytesty" ];
  result   =  [ "VERY", "DIFFERENT" ];
  assert_lists_of_strings_are_equal( expected, result );
};
# test_assert_lists_of_strings_are_equal();



fun assert_bools_are_equal( expected: Bool,  result: Bool ):   Bool = {
  if ( expected == result )
    TRUE;
  else
    print "\n  ** FAIL **\n";
    print "expected ";
    if ( expected ) print "TRUE"; else print "FALSE"; fi;
    print ", result ";
    if ( result ) print "TRUE\n"; else print "FALSE\n"; fi;
    FALSE;
  fi;
};
fun test_assert_bools_are_equal( _: Void ):  Bool = {
  expected =  TRUE;
  result   =  TRUE;
  assert_bools_are_equal( expected, result );

  # Should fail TRUE => FALSE
  expected =  TRUE;
  result   =  FALSE;
  assert_bools_are_equal( expected, result );

  # Should fail FALSE => TRUE
  expected =  FALSE;
  result   =  TRUE;
  assert_bools_are_equal( expected, result );
};
# test_assert_bools_are_equal();



# --
# --  Library-candidate functions, general
# --

fun check_directory( string: String ):  Bool = {
  # See also http://mythryl.org/my-Get_current_directory.html
  # getcwd ();

  directory_list =  dir::entries''( "." );
  # TODO:  Is it necessary to sort dir::entries  ?
  directory_list =  strsort( directory_list );
  current_directory =  head( directory_list );
  string == current_directory;
};
fun test_check_directory( _: Void ):  Void = {
  dir =  "/mnt/ssd/mythryl/git/compiled-website/.";
  result =  check_directory( dir );
  if result
    print "yes\n";
  else
    print "no\n";
  fi;
};
# test_check_directory();



# TODO:  Only display the current directory's name.  (separate function?)
fun get_current_directory( _: Void ):  String = {
  directory_list =  dir::entries''( "." );
  # TODO:  Is it necessary to sort dir::entries  ?
  directory_list =  strsort( directory_list );
  head( directory_list );
};
fun test_get_current_directory( _: Void ):  Void = {
  result =  get_current_directory();
  printf "%s\n" result;
};
# test_get_current_directory();



# FIXME:  This needs a better name.
# Removes the path from a string, giving only the filename.
fun get_filename( string: String ):  String = {
  # TODO:  I don't need to check for groups here, is there some other regex I should use?
  # FIXME Windows:  I need some sort of __SEPARATOR__ variable, which understands "/" on Linux, and "\" on Windows.
  strcat( regex::find_first_groups_all  ".*\\/(.*)"  string );
};
fun test_get_filename( _: Void ):  Bool = {
  string =  "/foo/bar/baz/filename.ext";
  expected =             "filename.ext";
  result = get_filename( string );
  assert_strings_are_equal( expected, result );

  # TODO:  Additional testing.
};
# test_get_filename();



# FIXME:  RENAME
# Removes the extension(s) from a string.
#   filename.ext     => filename
#   filename.ext.ext => filename
fun remove_extension( filename: String ):  String = {
  regex::replace_first  ./\..*$/  ""  filename;
};
fun test_remove_extension( _: Void ):  Bool = {
  string   = "filename.ext";
  expected = "filename";
  result = remove_extension( string );
  assert_strings_are_equal( expected, result );

  # TODO:  Additional testing.
};
# test_remove_extension();



fun string_multiplier( string: String,  n: Int ):  String = {
  cat( list::tabulate( n, fn _ = string ) );
};
fun test_string_multiplier( _: Void ):  Bool = {
  string   = "foo";
  n        = 2;
  expected = "foofoo";
  result = ( string_multiplier( string, n ) );
  assert_strings_are_equal( expected, result );

  string   = "foo";
  n        = 2;
  expected = "FAIL TEST";
  result = ( string_multiplier( string, n ) );
  assert_strings_are_equal( expected, result );
};
# test_string_multiplier();



fun is_even( i: Int ):  Bool = {
  if ( ( i % 2 ) == 0 )
    TRUE;
  else
    FALSE;
  fi;
};
fun test_is_even( _: Void ):  Bool = {
  n =  2;
  expected =  TRUE;
  result =  is_even( n );
  assert_bools_are_equal( expected, result );

  n =  1;
  expected =  FALSE;
  result =  is_even( n );
  assert_bools_are_equal( expected, result );

  # Meant to fail.
  n =  1;
  expected =  TRUE;
  result =  is_even( n );
  assert_bools_are_equal( expected, result );
};
# test_is_even();


fun is_odd( i: Int ):  Bool = {
  if ( ( i % 2 ) == 0 )
    FALSE;
  else
    TRUE;
  fi;
};
fun test_is_odd( _: Void ):  Bool = {
  n =  1;
  expected =  TRUE;
  result =  is_odd( n );
  assert_bools_are_equal( expected, result );

  # Meant to fail.
  n =  2;
  expected =  FALSE;
  result =  is_odd( n );
  assert_bools_are_equal( expected, result );

  # Meant to fail.
  n =  2;
  expected =  TRUE;
  result =  is_odd( n );
  assert_bools_are_equal( expected, result );
};
# test_is_odd();



# TODO:  Replace this with something easier to understand, using the standard take_n, drop_n and whatnot.
fun replace_nth_string( n: Int,  replacement: String,  list: List( String ) ):   List( String ) = {
    i =       REF -1;
    result =  REF [];

    foreach list .{
      i :=  *i + 1;
      if ( *i == n )
        result := *result @ [ replacement ];
      else
        result := *result @ [ #e ];
      fi;
    };

    *result;
};
# TODO:  fun test_replace_nth_string( foo ):   bar = {}
# list =  [ "This ", "is ", "a ", "test.\n" ]; 
# result =  replace_nth_string( 1, "was ", list );
# apply  printf "%s" result; 



# string_to_list() ideas
# TODO?:  Consider the existing string_to_list, maybe it can do what I want.
#         http://mythryl.org/my-String_To_List.html
#         String_To_List { first, between, last, string };

# Shit, this is way too complex to bother finishing.  There has to be something like this already available..
# I wonder if there is an "apply" or "map" usable on strings, to treat it as a list of characters.
#   match_character = "\n";
#   result =  REF [];
#   list =  explode( "abc\ndef\n" );
# Ugh, I'd have to turn the list of chars into a list of strings.
#   list =  reverse( list );
#   foreach list .{
#     # Build `result` based on if the character matches.
#     if ( #e == match_character )
#     else
#     fi;
#     printf "%c\n" #e;
#   };
# apply  printf "'%s'\n" *result;

fun string_to_list( string: String ):   List( String ) = {
  result =  REF [];

  if ( ( string =~ "\n" ) == FALSE )
    result :=  [ string ];
  else
    list =  tokens .{ #c == '\n'; } string;
    foreach list .{
      result :=  *result @ [ #e + "\n" ];
    };
  fi;

  *result;
};
# FIXME:               ( _: Void ):   List( String ) ={
fun test_string_to_list( _: Void ) = {
  # Simple
  string =  "foo";
  expected =  [ "foo" ];
  result =  string_to_list( string );
  assert_lists_of_strings_are_equal( expected, result );

  # Simple
  string =  "foo\n";
  expected =  [ "foo\n" ];
  result =  string_to_list( string );
  assert_lists_of_strings_are_equal( expected, result );

  string =  "HERE\nytesty\n";
  expected =  [ "HERE\n", "ytesty\n" ];
  result =  string_to_list( string );
  assert_lists_of_strings_are_equal( expected, result );
};
# test_string_to_list();



# Append blank elements to the shorter list to make it the same length.
# TODO:  Can I make this type-agnostic.
fun pad_string_list( list_one: List( String ),  list_two: List( String ) ):  List( List( String ) ) = {
  list_one =  REF list_one;
  list_two =  REF list_two;
  # Given two lists.
  # Determine which is longer.
  # Determine how much longer it is.
  l1 =  list::length( *list_one );
  l2 =  list::length( *list_two );
  if   ( l1 >  l2 )
    ++l2;
    foreach ( l2..l1 ) .{
      throwaway =  #i;
      # TODO:  If I wanted to make this type-agnostic, I'd have to learn how to append a blank element.
      list_two :=  *list_two @ [ "" ];
    };
  elif ( l1 <  l2 )
    ++l1;
    foreach ( l1..l2 ) .{
      throwaway =  #i;
      list_one :=  *list_one @ [ "" ];
    };
  fi;

  [ *list_one, *list_two ];
};
fun test_pad_string_list( _: Void ):   Bool = {
  list_one =  [ "one" ];
  list_two =  [ "one", "two" ];
  pad_string_list =  pad_string_list( list_one, list_two );
  list_one =  list::nth( pad_string_list, 0 );
  list_two =  list::nth( pad_string_list, 1 );

  expected =  [ "one", "" ];
  result =  list_one;
  assert_lists_of_strings_are_equal( expected, result );
};
# test_pad_string_list();



# --
# --  Library-candidate functions, project-specific
# --



# ---
# ---  Actual programming
# ---

fun read_print( filename: String ):   Void = {
  # Read from a source file.
  # http://mythryl.org/my-File.html
  # http://mythryl.org/my-Read_a_text_file.html
  if ( exists( filename ) )
    stream =  open_for_read( filename );
    # TODO:  Can I read it?  http://mythryl.org/my-Check_File_Readability.html
    # can_read : (Input_Stream , Int) -> Null_Or(Int );
    list =  read_lines( stream );
    apply printf "%s" list;
  fi;
};
fun test_read_print( _: Void ):   Void = {
  read_print( "compiled-website.txt" );
};
# test_read_print();


# Write to a target file.
# http://mythryl.org/my-File.html
# http://mythryl.org/my-Write_a_text_file.html
# TODO:  In the docs, what is a "Writer" ?
# I can't realistically convert this into a two-parameter function.
#   Basically I'd have to pass one List of Strings - the filename, and the content.
#   (This is probably not true, but I don't know any better)
#   But why do something as complex as that?  Just use the original simpler code.
fun write_file( _: Void ):   Void = {
  filename =  "test-write.txt";

  file = file::open_for_write filename;
  list = [ "line one\n", "line two" ];
  apply fprintf file "%s" list;

  # file::write       ( file, "There is no royal road to mathematics.\n" );
  # fprintf             file  "Mathematics is the %s of the sciences.\n" "queen";
  file::flush         file;
  file::close_output  file;

  # Display its contents.
  if ( exists( filename ) )
    printf "%s contains:\n--v\n" filename;

    stream =  open_for_read( filename );
    list =  read_lines( stream );
    apply printf "%s" list;
    winix::file::remove_file( filename );
    print "--^\n";
  else
    printf "%s was not created!\n" filename;
  fi;
};
fun test_write_file( _: Void ):   Void = {
  write_file();
};
# test_write_file();



# In source directory
#   check if the equivalent file exists in the target directory.
#   if no, re-create it.

# FIXME:  Sanity checking is impossible in this manner, because the initial "if ( winix::etc" is given an invalid directory then it'll just die then and there without passing false.  What the fuck.
# TODO:  Proper sanity-checking.
#        How would I stop script processing if the checking finds a critical error?
fun sanity_check( _: Void ):   Void = {
  if ( winix::file::is_directory( source_directory ) == FALSE )
    printf "Directory does not exist:  \"%s\"\n"  source_directory;
    # Make it?
  fi;
};
# sanity_check();


fun markup( string: String ):   String = {
  # TODO:  RENAME
  fun markup_needs_a_name( string: String,  rx_left: String,  rx_right: String,  replace_left: String,  replace_right: String ):   String = {
    fun punctuation_regex( rx_left: String,  rx_right: String ):   String = {
      # TODO:  Why can't I directly use [:graph:], [:space:], etc?
      graph =  "[\\x21-\\x7E]";   # [\x21-\x7E]  =  [:graph:]  =  printable characters, also doable as [^\s] (not whitespace)
      punct =  "[^\\32-\\151]";   # [^\32-\151]  =  [:punct:]  =  punctuation
      space =  "\\s";             # \s           =  [:space:]  =  whitespace

      left =
          "^"
        + "|" + space
        ;
      left =
          left
        + "|" + left + punct
        + "|" + left + "\\("
        + "|" + left + "--"
      ;

      right =
          "$"
        + "|" + space
        ;
      right =
          right
        + "|" + punct + right
        + "|" + "\\)" + right
        + "|" + "--"  + right
      ;
      right =
          right
        + "|" + "s"  + right
        + "|" + "es" + right
        + "|" + "ed" + right
      ;
      "(" + left + ")(" + rx_left + ")(.*)(" + rx_right + ")(" + right + ")";
    };

    # TODO:    string =  split_string_html( string );

    rx = punctuation_regex( rx_left, rx_right );
    string =  regex::replace_all_via_fn  rx .{
        ( list::nth( #stringlist, 0 ) )
      + replace_left
      + ( list::nth( #stringlist, 2 ) )
      + replace_right
      + ( list::nth( #stringlist, 4 ) )
      ;
    } string;
    string;
  };



  fun markup_horizontal_rules( string: String ):   String = {
    # With an empty space above and below.
    fun markup_horizontal_rules_long( string: String ):   String = {
      regex::replace_all  ./\n{2}\-{3,}\n{2}/  "\n<hr class=\"long\">\n" string;
    };

    fun markup_horizontal_rules_short( string: String ):   String = {
      # (with content above)
      string =  regex::replace_all_via_fn  ./([^\n])\n-{3,}\n{2}/ .{ strcat( #stringlist ) + "<hr class=\"small\">\n"; } string;
      # (with content below)
      string =  regex::replace_all_via_fn  ./\n{2}-{3,}\n([^\n])/ .{ "\n<hr class=\"small\">" + strcat( #stringlist ); } string;
      string;
    };

    string =  markup_horizontal_rules_long ( string );
    string =  markup_horizontal_rules_short( string );
    string;
  };



  fun markup_paragraphs( string: String ):   String = {
    # \n\n  =  Paragraph
    regex::replace_all_via_fn  ./(\n{2,})/ .{
      n =  strlen( strcat( #stringlist ) );
      string_multiplier( "\n<br />", n );
    } string;
  };


  string =  markup_needs_a_name( string, "\\/", "\\/", "<em>"    , "</em>"     );
  string =  markup_needs_a_name( string, "-"  , "-"  , "<del>"   , "</del>"    );
  string =  markup_needs_a_name( string, "\\*", "\\*", "<strong>", "</strong>" );
  string =  markup_needs_a_name( string, "_"  , "_"  , "<u>"     , "</u>"      );
  string =  markup_horizontal_rules( string );
  string =  markup_paragraphs( string );
  string;
};



fun rebuild_template( template_filename: String,  compiling_filename: String ):   Void = {
  if ( exists( template_filename ) == FALSE )
    print ( "File doesn't exist!: " + template_filename );
  else
    # IDEA:  It would be simple to have specific search/replace for a specific file.
    stream =  open_for_read( template_filename );
    # TODO sanity:  Can I read it?  http://mythryl.org/my-Check_File_Readability.html
    # can_read : (Input_Stream , Int) -> Null_Or(Int );
    list =  read_lines( stream );

    page_title =  get_filename( compiling_filename );
    page_title =  remove_extension( page_title );
    # TODO:  Table of Contents
    # FIXME:  Browser, when clicking "Table of Contents", that text seems to wiggle.
    #         (Probably a template issue, possibly unfixable)
    toc =  "<br>TO DO";
    # TODO:  RSS
    #        rss-feed-icon-16px-svg.png
    rss =  "";

    # TODO:  Beautify / simplify.  Use a case-regex?
    list =  map ( fn string = regex::replace_all   "<!--SITE NAME-->"     site_name     string ) list;
    list =  map ( fn string = regex::replace_all   "<!--SITE TAGLINE-->"  site_tagline  string ) list;
    list =  map ( fn string = regex::replace_all   "<!--TITLE-->"         page_title    string ) list;
    list =  map ( fn string = regex::replace_all   "<!--TOC-->"           toc           string ) list;
    list =  map ( fn string = regex::replace_all   "<!--RSS-->"           rss           string ) list;

    template_filename =  template_filename + "-rebuilt.html";
    file = file::open_for_write template_filename;
    apply fprintf file "%s" list;
    file::flush         file;
    file::close_output  file;
  fi;
};



# TODO:  Think up a better name.
fun post_processing( content_string: String ):   String = {
  fun post_processing_redirect( content_string: String ):   String = {
    match =  "<!--REDIRECT (.*)-->";
    if ( ( content_string =~ match ) == FALSE )
      content_string;
    else
      page_name = regex::find_first_group 1  match  content_string;
      content_string =  regex::replace_all  match  ( "<p>Redirecting to " + page_name + " ...</p>" )  content_string;
      replace =  "<head><meta http-equiv=\"refresh\" content=\"2;url=" + page_name + "\">";
      content_string =  regex::replace_first  "<head>"  replace  content_string;
      content_string;
    fi;
  };

  content_string = post_processing_redirect( content_string );

  content_string;
};



fun rebuild( compiling_filename: String,  target_filename: String ):   Void = {
  if ( exists( compiling_filename ) == FALSE )
    print ( "File doesn't exist!: " + compiling_filename );
  else
    rebuild_template( header, compiling_filename );
    rebuild_template( footer, compiling_filename );

    content_string =                                 strcat( lines( header + "-rebuilt.html" ) );
    content_string =  content_string + markup( strcat( lines( compiling_filename ) ) );
    content_string =  content_string +               strcat( lines( footer + "-rebuilt.html" ) );
    content_string =  post_processing( content_string );

    f =  file::open_for_write  target_filename;
    file::write       ( f, content_string );
    file::flush         f;
    file::close_output  f;
  fi;
};



rebuild( 
  source_directory + "sandbox.asc",
  target_directory + "sandbox.html"
);


# rebuild(
#   source_directory + "../index.asc",
#   target_directory + "../index.html"
# );




# --
# --  Test cases
# --  (more or less)
# TODO:  Absorb the test cases from the Ruby project.


string =  "test";
expected =  "test";
result =  markup( string );
assert_strings_are_equal( expected, result );

string =  "\n/emphasis/\n";
expected =  "\n<em>emphasis</em>\n";
# FIXME:  Markup is chopping off the final \n.
expected =  "\n<em>emphasis</em>";
result =  markup( string );
assert_strings_are_equal( expected, result );

string =  "/emphasis/";
expected =  "<em>emphasis</em>";
result =  markup( string );
assert_strings_are_equal( expected, result );



# Take a string, and break it into a list of strings.  That list of strings is odd-even, where odd is of one type and even is of another type.
# The idea is to break a string down so that a function can be applied to specific parts of it.
# For example, I want to apply markup, but not to text inside of blocks of html.
fun block_matcher( string: String ):   List( String ) = {
  list =  string_to_list( string );

  fun block_matcher_bool( string: String ):   Bool = {
    # TODO:  If the regex is a match.
    if ( string =~ ./^\ *\-/ )
      TRUE;
    else
      FALSE;
    fi;
  };

  result   =  REF [];
  buffer   =  REF "";
  # We begin at an even position, element 0 in the `result` list.
  position =  REF "even";

  # Iterate through the list of strings.
  foreach list .{
    # For each line, refer to a function which gives a Bool result.
    bool =  block_matcher_bool( #e );
    # Based on the Bool, either append to a working string or append to the list and begin an empty working string.

    if ( bool == TRUE )
      if ( *position == "even" )
        # We have a match, but we're in the even position.  Matches are supposed to be placed in the odd position.

        # Dump the buffer into the last element of the result.
        result := *result @ [ *buffer ];
        buffer := "";

        # Switch to the odd position.
        position := "odd";
      fi;
    else  # ( bool == FALSE )
      if ( *position == "odd" )
        # We don't have a match, but we're still in the odd position.  Non-matches are supposed to be placed in the even position.

        # Dump the buffer into the last element of the result.
        result := *result @ [ *buffer ];
        buffer := "";

        # Switch to the odd position.
        position := "even";
      fi;
    fi;
    # Append to the buffer.
    buffer := *buffer + #e;

#     print "--v\n";
#     printf "#e       '%s'\n" #e;
#     printf "*buffer  '%s'\n" *buffer;
#     print "*result\n";
#     apply  printf "'%s'\n" *result;
#     print "--^\n";
  };

  *result @ [ *buffer ];
};

# Not a match
string =  "nomatch";
expected =  [ "nomatch" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Only a match
string =  "- bullet";
expected =  [ "", "- bullet" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Match, not a match
string =  "- bullet\nytesty\n";
expected =  [ "", "- bullet\n", "ytesty\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Not a match, match
string =  "ytesty\n- bullet\n";
expected =  [ "ytesty\n", "- bullet\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Two consecutive matches only
string =  "- bullet\n- bullet\n";
expected =  [ "", "- bullet\n- bullet\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Two consecutive matches, not a match
string =  "- bullet\n- bullet\nytesty\n";
expected =  [ "", "- bullet\n- bullet\n", "ytesty\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Not a match, two consecutive matches, not a match
string =  "foo\n- bullet\n- bullet\nytesty\n";
expected =  [ "foo\n", "- bullet\n- bullet\n", "ytesty\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Not a match, two consecutive matches
string =  "foo\n- bullet\n- bullet\n";
expected =  [ "foo\n", "- bullet\n- bullet\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# Oh hell, let's get silly.
string =  "foo\n- bullet\n- bullet\nbar\nbaz\nargh\n- bullet\nbla\n- bullet\n";
expected =  [ "foo\n", "- bullet\n- bullet\n", "bar\nbaz\nargh\n", "- bullet\n", "bla\n", "- bullet\n" ];
result =  block_matcher( string );
assert_lists_of_strings_are_equal( expected, result );

# FIXME:  string_to_list() chops out any duplicate \n like here             ---v
# string =  "foo\n- bullet\n- bullet\nbar\nbaz\nargh\n- bullet\nbla\n- bullet\n\n";
# expected =  [ "foo\n", "- bullet\n- bullet\n", "bar\nbaz\nargh\n", "- bullet\n", "bla\n", "- bullet\n\n" ];
# result =  block_matcher( string );
# assert_lists_of_strings_are_equal( expected, result );
